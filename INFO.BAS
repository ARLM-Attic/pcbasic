10  REM INFO.BAS - PC-BASIC 3.23 Information
11  REM Copyright (c) 2013, 2014 Rob Hagemans
12  REM Released under the GNU GPL version 3
19  '
21  KEYS.LEN=7: KEYS.NAME$(1)="1": KEYS.WHAT$(1)="About": N$(1)="ABOUT"
22  KEYS.NAME$(2)="2": KEYS.WHAT$(2)="Legal": N$(2)="COPYING"
23  KEYS.NAME$(3)="3": KEYS.WHAT$(3)="GPLv3": N$(3)="GPL3"
24  KEYS.NAME$(4)="4": KEYS.WHAT$(4)="Documentation": N$(4)="HELP"
25  KEYS.NAME$(5)="Q": KEYS.WHAT$(5)="Quit"
26  KEYS.NAME$(6)="S": KEYS.WHAT$(6)="Search"
27  KEYS.NAME$(7)="ENTER": KEYS.WHAT$(7)="Next"
30  '
31  SCREEN 0,0,0:CLS:KEY OFF:VIEW PRINT 1 TO 24
32  NM$="ABOUT": GOSUB 13000
33  WHILE -1
34    SCREEN 0,0,0:CLS:KEY OFF: COUNT=0
65    GOSUB 10000
129   GOSUB 12000
130   GOSUB 10200
134   IF K$="S" THEN GOSUB 1300: GOTO 65
136   IF K$="Q" THEN GOSUB 1400
137   IF K$=CHR$(13) THEN GOTO 160
140   K=0: IF K$>"0" AND K$<="9" THEN IF VAL(K$)<KEYS.LEN THEN K=VAL(K$)
151   IF K<>0 THEN NM$=N$(K): GOSUB 13000
160 WEND 
990 END 
999 '
1300 REM search
1330   VIEW PRINT:LOCATE 25,1:COLOR 0,7:PRINT SPC(79);
1335   LOCATE 25,3:PRINT "Search: ";:COLOR 7,0:INPUT ;"", COMMAND$
1339   VIEW PRINT 1 TO 24:CLS 
1340   GOSUB 10300
1390 RETURN 
1399 '
1400 REM exit
1405   SCREEN 0,0,0:CLS:KEY ON 
1410   END 
9999 '
10000 REM display text file
10020   WHILE NOT EOF(1) AND COUNT<23
10030     LINE INPUT #1, LIN$
10040     PRINT LIN$;:IF CSRLIN<COUNT+2 THEN PRINT CHR$(13);
10050     COUNT=COUNT+1
10070   WEND 
10110 RETURN 
10199 '
10200 REM Press a key
10220   K$="":WHILE K$="":K$=INKEY$:WEND 
10230   IF K$>="a" AND K$<="z" THEN K$=CHR$(ASC(K$)-32)
10240 RETURN 
10299 '
10300 REM Find command in text file, then display
10305   CLOSE #1
10310   OPEN NM$ FOR INPUT AS #1
10320   WHILE NOT EOF(1)
10330     LINE INPUT #1, LIN$
10340     IF LEFT$(LIN$,LEN(COMMAND$))=COMMAND$ THEN PRINT LIN$: COUNT=1: GOTO 10390
10370   WEND 
10390 RETURN 
10999 '
12000 REM bottom line
12010   VIEW PRINT:LOCATE 25,1
12020   FOR I=1 TO KEYS.LEN
12030     PRINT KEYS.NAME$(I);
12035     COLOR 0,7:PRINT " " KEYS.WHAT$(I); "  ";:COLOR 7,0
12040   NEXT 
12050   COLOR 0,7:PRINT SPC(81-POS(0));:COLOR 7,0: VIEW PRINT 1 TO 24
12060 RETURN 
12999 '
13000 REM swap files
13010 CLOSE #1
13020 OPEN NM$ FOR INPUT AS 1
13030 RETURN 
